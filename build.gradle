plugins {
	id 'fabric-loom' version '1.1-SNAPSHOT'
	id "io.github.juuxel.loom-quiltflower" version "1.+"
	id "io.github.p03w.machete" version "1.+"
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

repositories {
	mavenLocal()
	mavenCentral()
	maven { url = "https://mvn.devos.one/snapshots/" }// Create, Porting Lib, Forge Tags, Milk Lib, Registrate
	maven { url = "https://api.modrinth.com/maven" }
	maven { url = "https://maven.tterrag.com/" }// Flywheel
	maven { url = "https://maven.shedaniel.me/" }// Cloth Config, REI
	maven { url = "https://jitpack.io/" }// Mixin Extras, Fabric ASM
	maven { url = "https://maven.jamieswhiteshirt.com/libs-release" }// Reach Entity Attributes
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation("com.simibubi.create:create-fabric-${minecraft_version}:${create_version}") {
		exclude(Map.of("module", "forge-config-api-port-fabric-547434"))
	}
	modImplementation("io.github.fabricators_of_create.Porting-Lib:porting-lib:${port_lib_version}+${minecraft_version}")
	modLocalRuntime("maven.modrinth:forge-config-api-port:4.2.11")

	modImplementation("maven.modrinth:farmers-delight-fabric:1.3.9")
	modCompileOnly("me.shedaniel:RoughlyEnoughItems-fabric:9.1.580")
	modLocalRuntime("maven.modrinth:enchlevel-langpatch:2.1.0")
	modImplementation fileTree(dir: 'libs', include: ['*.jar'])
}

base {
	archivesName = project.archives_base_name
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
